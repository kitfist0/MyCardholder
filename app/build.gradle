plugins {
    alias(libs.plugins.android.application)
    alias(libs.plugins.firebase.crashlytics)
    alias(libs.plugins.gms)
    alias(libs.plugins.hilt.android)
    alias(libs.plugins.kotlin.android) // safeargs doesn't work without kotlin-android plugin!
    alias(libs.plugins.ksp)
    alias(libs.plugins.safeargs.kotlin)
    alias(libs.plugins.secrets.gradle)
}

import com.android.build.gradle.internal.tasks.FinalizeBundleTask
import org.jetbrains.kotlin.gradle.dsl.JvmTarget

secrets {
    // Ignore all keys matching regex "sdk.*"
    ignoreList.add("sdk.*")
}

android {
    compileSdkVersion 36

    namespace 'my.cardholder'

    defaultConfig {
        applicationId "my.cardholder"
        applicationIdSuffix ".gp"
        minSdk 24
        targetSdk 35
        versionCode 20
        versionName "v1.1.7"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        buildConfigField("String", "APP_NAME", "\"MyCardholder\"")
        buildConfigField("String", "DEV_NAME", "\"MidnightDev\"")
        buildConfigField("String", "YEAR", "\"2026\"")

        def buildTime = new Date()
        buildConfigField "String", "BUILD_TIME", "\"${buildTime.format('yyyy-MM-dd HH:mm')}\""
    }

    flavorDimensions = ["billing"]

    productFlavors {
        googleplay {
            dimension "billing"
            buildConfigField("String", "APP_LOGO_LINK", "\"https://play-lh.googleusercontent.com/CukdHWulsmo9nwmiLmJg9tViyUv2UfWV5KUMw-Emm-wnAmKfjItajKidbdRw0MR8i6o=w480-h960-rw\"")
            buildConfigField("String", "APP_STORE_NAME", "\"GooglePlay\"")
            buildConfigField("String", "WEB_PAGE_STORE", "\"https://play.google.com\"")
            buildConfigField("String", "WEB_PAGE_LICENSE", "\"https://github.com/kitfist0/MyCardholder/blob/master/LICENSE\"")
            buildConfigField("String", "WEB_PAGE_POLICY", "\"https://github.com/kitfist0/MyCardholder/blob/master/POLICY.md\"")
            buildConfigField("String", "WEB_PAGE_REPO", "\"https://github.com/kitfist0/MyCardholder\"")
        }
        rustore {
            dimension "billing"
            buildConfigField("String", "APP_LOGO_LINK", "\"https://static.rustore.ru/imgproxy/GtqXx4sVVn6YA0TTHnqZrAPsmyb6iGt2Y4eZ0auW-WQ/preset:web_app_icon_160/plain/https://static.rustore.ru/apk/2063489959/content/ICON/a948ad25-95c0-4fda-88f3-25968f431e0c.png@webp\"")
            buildConfigField("String", "APP_STORE_NAME", "\"RuStore\"")
            buildConfigField("String", "WEB_PAGE_STORE", "\"https://rustore.ru\"")
            buildConfigField("String", "WEB_PAGE_LICENSE", "\"https://gitverse.ru/kitfist0/MyCardholder/content/master/LICENSE\"")
            buildConfigField("String", "WEB_PAGE_POLICY", "\"https://gitverse.ru/kitfist0/MyCardholder/content/master/POLICY.md\"")
            buildConfigField("String", "WEB_PAGE_REPO", "\"https://gitverse.ru/kitfist0/MyCardholder\"")

            // Don't forget to specify Yandex ClientID in ./local.properties! As shown below:
            // YANDEX_CLIENT_ID=0123456789abcdef0123456789abcdef
            Properties properties = new Properties()
            properties.load(project.rootProject.file('local.properties').newDataInputStream())
            def yandexClientIdToken = properties.getProperty('YANDEX_CLIENT_ID')
            manifestPlaceholders = [YANDEX_CLIENT_ID: yandexClientIdToken]
        }
    }

    signingConfigs {
        create("common") {
            storeFile = rootProject.file('cert/release.keystore')
            def propsFile = rootProject.file('cert/release.properties')
            def props = new Properties()
            props.load(new FileInputStream(propsFile))
            storePassword = props['store_password']
            keyAlias = props['alias']
            keyPassword = props['key_password']
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            signingConfig = signingConfigs.getByName("common")
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig = signingConfigs.getByName("common")
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlin {
        compilerOptions {
            jvmTarget = JvmTarget.JVM_17
        }
    }

    buildFeatures {
        buildConfig = true
        viewBinding = true
    }

    // Rename APKs and AABs
    applicationVariants.configureEach { variant ->
        def artifactName = "${variant.flavorName.toLowerCase()}-${variant.versionCode}-${variant.buildType.name}"
        variant.outputs.configureEach {
            outputFileName = "${artifactName}.apk"
        }
        tasks.named("sign${variant.name.capitalize()}Bundle", FinalizeBundleTask) {
            File file = finalBundleFile.asFile.get()
            File finalFile = new File(file.parentFile, "${artifactName}.aab")
            finalBundleFile.set(finalFile)
        }
    }

    ksp {
        arg('room.schemaLocation', "$projectDir/schemas")
    }

    packagingOptions {
        resources.excludes += 'META-INF/DEPENDENCIES'
        resources.excludes += 'META-INF/INDEX.LIST'
        resources.excludes += 'META-INF/LICENSE'
        resources.excludes += 'META-INF/LICENSE.txt'
        resources.excludes += 'META-INF/license.txt'
        resources.excludes += 'META-INF/NOTICE'
        resources.excludes += 'META-INF/NOTICE.txt'
        resources.excludes += 'META-INF/notice.txt'
        resources.excludes += 'META-INF/ASL2.0'
        resources.excludes += 'META-INF/*.kotlin_module'
    }
}

dependencies {
    implementation(libs.androidx.activity.ktx)
    implementation(libs.androidx.appcompat)
    implementation(libs.androidx.camera.camera2)
    implementation(libs.androidx.camera.core)
    implementation(libs.androidx.camera.lifecycle)
    implementation(libs.androidx.camera.view)
    implementation(libs.androidx.concurrent.futures)
    implementation(libs.androidx.constraintlayout)
    implementation(libs.androidx.core.ktx)
    implementation(libs.androidx.datastore.preferences)
    implementation(libs.androidx.lifecycle.viewmodel)
    implementation(libs.androidx.navigation.fragment.ktx)
    implementation(libs.androidx.navigation.ui.ktx)
    implementation(libs.androidx.preference.ktx)
    ksp(libs.androidx.room.compiler)
    implementation(libs.androidx.room.ktx)
    implementation(libs.androidx.room.runtime)
    implementation(libs.androidx.splashscreen)
    implementation(libs.androidx.work.runtime)

    // Billing
    googleplayImplementation(libs.googleplay.billingclient)
    rustoreImplementation(platform(libs.rustore.bom))
    rustoreImplementation(libs.rustore.pay)

    // Coil
    implementation(libs.coil)
    implementation(libs.coil.base)
    implementation(libs.coil.svg)

    // Drive
    implementation(libs.google.api.client.android) {
        exclude group: 'org.apache.httpcomponents'
    }
    implementation(libs.google.api.services.drive) {
        exclude group: 'org.apache.httpcomponents'
    }
    implementation(libs.google.http.client.gson)
    implementation(libs.google.play.services.auth)

    // Firebase
    implementation platform(libs.google.firebase.bom)
    implementation(libs.google.firebase.analytics)
    implementation(libs.google.firebase.crashlytics)

    // Flexbox
    implementation(libs.google.flexbox)

    // Material
    implementation(libs.google.material)

    // Ml Kit
    implementation(libs.google.mlkit)

    // Hilt
    implementation(libs.hilt.android)
    ksp(libs.hilt.compiler)

    // Image Cropper
    implementation(libs.image.cropper)

    // Kotlin CSV
    implementation(libs.kotlin.csv)

    // Okhttp and Retrofit
    implementation(libs.squareup.okhttp3.okhttp)
    implementation(libs.squareup.retrofit2.retrofit)
    implementation(libs.squareup.retrofit2.converter)

    // Yandex
    implementation(libs.yandex.authsdk)

    // Zxing
    implementation(libs.zxing.core)

    testImplementation(libs.junit)
    androidTestImplementation(libs.androidx.test.ext)
    androidTestImplementation(libs.androidx.test.espresso.core)
}
