plugins {
    alias(libs.plugins.android.application)
    // alias(libs.plugins.firebase.crashlytics)
    // alias(libs.plugins.gms)
    alias(libs.plugins.hilt.android)
    alias(libs.plugins.kotlin.android)
    alias(libs.plugins.kotlin.kapt)
    alias(libs.plugins.ksp)
    alias(libs.plugins.safeargs.kotlin)
    alias(libs.plugins.secrets.gradle)
}

import com.android.build.gradle.internal.tasks.FinalizeBundleTask
import org.jetbrains.kotlin.gradle.dsl.JvmTarget

secrets {
    // Ignore all keys matching regex "sdk.*"
    ignoreList.add("sdk.*")
}

android {
    compileSdkVersion 36

    namespace = 'my.cardholder'

    defaultConfig {
        applicationId "my.cardholder"
//        applicationIdSuffix ".gp"
        minSdk 24
        targetSdk 35
        versionCode 19
        versionName "1.0.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        buildConfigField("String", "APP_NAME", "\"Wallet\"")
        buildConfigField("String", "DEV_NAME", "\"defqy\"")
        buildConfigField("String", "YEAR", "\"2026\"")

        buildConfigField("String", "WEB_PAGE_STORE", "\"https://play.google.com\"")
        buildConfigField("String", "APP_STORE_NAME", "\"GitHub\"")
        buildConfigField("String", "WEB_PAGE_LICENSE", "\"https://github.com/kitfist0/Wallet/blob/master/LICENSE\"")
        buildConfigField("String", "WEB_PAGE_POLICY", "\"https://github.com/kitfist0/Wallet/blob/master/POLICY.md\"")
        buildConfigField("String", "WEB_PAGE_REPO", "\"https://github.com/kitfist0/Wallet\"")
        buildConfigField("String", "APP_LOGO_LINK", "\"https://github.com/kitfist0/Wallet\"")

        def buildTime = new Date()
        buildConfigField "String", "BUILD_TIME", "\"${buildTime.format('yyyy-MM-dd HH:mm')}\""
    }

    signingConfigs {
        create("common") {
            storeFile = rootProject.file('cert/release.keystore')
            def propsFile = rootProject.file('cert/release.properties')
            def props = new Properties()
            props.load(new FileInputStream(propsFile))
            storePassword = props['store_password']
            keyAlias = props['alias']
            keyPassword = props['key_password']
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            // signingConfig = signingConfigs.getByName("common")
        }
        release {
//            minifyEnabled false
//            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
//            signingConfig = signingConfigs.getByName("common")
        }
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    kotlin {
        compilerOptions {
            jvmTarget = JvmTarget.JVM_17
        }
    }

    buildFeatures {
        buildConfig = true
        viewBinding = true
    }

    // Rename APKs and AABs
    applicationVariants.configureEach { variant ->
        def artifactName = "${variant.flavorName.toLowerCase()}-${variant.versionCode}-${variant.buildType.name}"
        variant.outputs.configureEach {
            outputFileName = "${artifactName}.apk"
        }
        tasks.named("sign${variant.name.capitalize()}Bundle", FinalizeBundleTask) {
            File file = finalBundleFile.asFile.get()
            File finalFile = new File(file.parentFile, "${artifactName}.aab")
            finalBundleFile.set(finalFile)
        }
    }

    ksp {
        arg('room.schemaLocation', "$projectDir/schemas")
    }

    packagingOptions {
        resources.excludes += 'META-INF/DEPENDENCIES'
        resources.excludes += 'META-INF/INDEX.LIST'
        resources.excludes += 'META-INF/LICENSE'
        resources.excludes += 'META-INF/LICENSE.txt'
        resources.excludes += 'META-INF/license.txt'
        resources.excludes += 'META-INF/NOTICE'
        resources.excludes += 'META-INF/NOTICE.txt'
        resources.excludes += 'META-INF/notice.txt'
        resources.excludes += 'META-INF/ASL2.0'
        resources.excludes += 'META-INF/*.kotlin_module'
    }
}

dependencies {
    implementation(libs.androidx.activity.ktx)
    implementation(libs.androidx.appcompat)
    implementation(libs.androidx.camera.camera2)
    implementation(libs.androidx.camera.core)
    implementation(libs.androidx.camera.lifecycle)
    implementation(libs.androidx.camera.view)
    implementation(libs.androidx.concurrent.futures)
    implementation(libs.androidx.constraintlayout)
    implementation(libs.androidx.core.ktx)
    implementation(libs.androidx.datastore.preferences)
    implementation(libs.androidx.lifecycle.viewmodel)
    implementation(libs.androidx.navigation.fragment.ktx)
    implementation(libs.androidx.navigation.ui.ktx)
    implementation(libs.androidx.preference.ktx)
    ksp(libs.androidx.room.compiler)
    implementation(libs.androidx.room.ktx)
    implementation(libs.androidx.room.runtime)
    implementation(libs.androidx.splashscreen)
    implementation(libs.androidx.work.runtime)

    // Coil
    implementation(libs.coil)
    implementation(libs.coil.base)
    implementation(libs.coil.svg)

    // Flexbox
    implementation(libs.google.flexbox)

    // Material
    implementation(libs.google.material)

    // Ml Kit
    implementation(libs.google.mlkit)

    // Hilt
    implementation(libs.hilt.android)
    kapt(libs.hilt.compiler)

    // Image Cropper
    implementation(libs.image.cropper)

    // Kotlin CSV
    implementation(libs.kotlin.csv)

    // Zxing
    implementation(libs.zxing.core)

    testImplementation(libs.junit)
    androidTestImplementation(libs.androidx.test.ext)
    androidTestImplementation(libs.androidx.test.espresso.core)
}
